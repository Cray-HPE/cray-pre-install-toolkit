#!ipxe

# Change this to any available URL:
set base-url http://spit

# If using a custom image or URL, modify this:
set image-name filesystem.squashfs

# Change this if your cloud-init server is elsewhere.
set cloud-init ds=nocloud-net\;s=${base-url}:8080

# Kernel Crash Helpers:
set kernel-crash-params bad_page=panic crashkernel=320M hugepagelist=2m-2g

# Necessary Booting SquashFS Booting Parameters:
set kernel-params initrd=initrd.img.xz root=crayurl:${base-url}/${image-name} ro rd.skipfsck ${kernel-crash-params}

# Matching NCN boot parameters:
set ncn-params pcie_ports=native transparent_hugepage=never biosdevname=1 console=tty0 console=ttyS0,115200 iommu=pt rd.multipath=0 rd.dm=0 rd.md=1 rd.auto=1

# Bond Parameters:
# To disable, comment out and provide a `lan0` interface.
# i.e. bridge:lan0:eth1:9238
set net-bond-params bond=lan0:net0,net1:mode=802.3ad,miimon=100,lacp_rate=fast,xmit_hash_policy=layer2+3:9238 hwprobe=+200:*:*:bond0

# Networking parameters:
# Note: Not using a VLAN? Switch the bootdev and disable vlan002 dhcp:
# sed -i '/bootdev=.*/bootdev=lan0' /var/www/script.ipxe
# sed -i '/ip=vlan002.*/ip=lan0' /var/www/script.ipxe
set net-params ifname=lan1:${net0/mac} ifname=lan3:${net1/mac} ifname=net0:${net2/mac} ifname=net1:${net3/mac} vlan=vlan002:lan0 vlan=vlan004:lan0 ip=vlan002:dhcp ip=vlan004:dhcp bootdev=vlan002 rd.bootif=0 ${net-bond-params}

# Parameters for CI/CD to ad-hoc replace (null by default):
# Good for "always-on" params for automation.
# sed -i '/robot-params .*/robot-params parm1 param2 param3' /var/www/script.ipxe
set robot-params append

# Custom parameters:
# Good for one-off changes/tests.
# example for a user or automation-toggle runs to replace on-the-fly:
# sed -i '/custom-params .*/custom-params parm1 param2 param3' /var/www/script.ipxe
set custom-params rd.shell rd.retry=10 nosplash quiet

# Emulate real world, and boot over VLANs.
# Note: This will also boot without VLAN, you'll be able to know by the IPs leased
echo Calling home...
:retry
dhcp || goto retry
echo Printing routing..
route
echo Priting neighbors..
nstat
ifstat

# Symlinks are followed; linking the default names to the versioned artifacts you want
# or moving the ${base_url} will avoid the need for modifying these strings.
kernel --name kernel ${base-url}/kernel ${ncn-params} ${cloud-init} ${kernel-params} ${net-params} ${robot-params} ${custom-params}
initrd ${base-url}/initrd.img.xz

boot
